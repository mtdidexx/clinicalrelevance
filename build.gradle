import java.text.SimpleDateFormat

plugins {
	id 'org.springframework.boot' version '2.3.3.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'com.google.cloud.tools.jib' version '2.4.0'
}

group = 'net.katrinka'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext {
	imageTag = project.hasProperty('commitSha') ? project.getProperty('commitSha') : new SimpleDateFormat('MMMdd-HHmmss').format(new Date())
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springdoc:springdoc-openapi-ui:1.5.8'
	implementation 'redis.clients:jedis:3.3.0'
	testImplementation "io.rest-assured:rest-assured-all:${property("restAssuredVersion")}"
	testImplementation "io.rest-assured:spring-mock-mvc:${property("restAssuredVersion")}"
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}
jib {
	from {
		image = "openjdk:13-alpine"
	}
	to {
		image = "gcr.io/gdos-mdonahue-sb/gdos-clinicalrelevance:${imageTag}"
	}
	container {
		mainClass = "net.katrinka.clinicalrelevance.ClinicalrelevanceApplication"
	}
}

wrapper {
	distributionType = Wrapper.DistributionType.ALL
}

task displayImageTag {
	doLast {
		println "Image tag: ${imageTag}"
	}
}